// use std::str::FromStr;
use crate::ast::{AstNode,Term};
use crate::tok::Tok;

grammar<'input>(input: &'input str);


pub Iris: Vec<AstNode> = {
    <s:TopLevel*> => s,
};

Semicolon<T>: Vec<T> = { // (1)
    <e:T> <v:( <T>)*> => { // (2)
        let mut ret = vec![e];
        let mut v = v;
        ret.append(&mut v);
        ret
    }
}

TopLevel: AstNode = {
    <f:FnDef> => f,
    <x:Assign> ";" => x,
    <x:IfStmt> => x,
    <e:Expr> ";" => e,
};

Comma<T>: Vec<T> = { // (1)
    <e:T> <v:("," <T>)*> => { // (2)
        let mut ret = vec![e];
        let mut v = v;
        ret.append(&mut v);
        ret
    }
}

FnDefArgs = Comma<Ident>;
FnBodyStmt: AstNode = {
    <t:TopLevel> => t,
    <x:Return> ";" => x,
}
FnDef: AstNode = {
    "fn" <name:Ident> "(" <args:FnDefArgs?> ")" "{" <body: FnBodyStmt*> "}" => AstNode::FnDef {
        name,
        args: match args {
            Some(a) => a,
            None => vec![],
        },
        body,
    }
}

// TODO: runtime to support anonymous fn defs
AnonymousFnDef: AstNode = {
    "fn" "(" <args:FnDefArgs?> ")" "{" <body: FnBodyStmt*> "}" => AstNode::FnDef {
        name: "anon".to_string(),
        args: match args {
            Some(a) => a,
            None => vec![],
        },
        body,
    }
}

Assign: AstNode = {
    <ident:Ident> "=" <e:Expr> => AstNode::Assignment(
        Term::Ident(ident),
        Box::new(e),
    )
}

Return: AstNode = {
    "return" <e:Expr> => AstNode::Return(Box::new(e)),
}

ElseIf: AstNode = {
    "elif" "(" <cond_expr:Expr> ")" "{" <body: FnBodyStmt*> "}" => AstNode::ElseIf {
        cond_expr: Box::new(cond_expr),
        body,
    }
}
Else: Vec<AstNode> = {
    "else" "{" <body: FnBodyStmt*> "}" => body,
}
IfStmt: AstNode = {
    "if" "(" <cond_expr:Expr> ")" "{" <body: FnBodyStmt*> "}" <else_if:ElseIf*> <else_body:Else?> => AstNode::If {
        cond_expr: Box::new(cond_expr),
        body,
        else_if,
        else_body: match else_body {
            Some(b) => b,
            None => vec![],
        }
    }
}

Expr: AstNode = {
    <f:FnCall> => f,
    <a:AnonymousFnDef> => a,
    <t:Term> => t,
}

Term: AstNode = {
    <n:Int> => AstNode::Term(Term::Integer(n)),
    <x:Ident> => AstNode::Term(Term::Ident(x)),
    <b:Bool> => AstNode::Term(Term::Bool(b)),
    <s:Str> => AstNode::Term(Term::Str(s)),
    "(" <t:Term> ")" => t,
}

FnCallArgs = Comma<Expr>; // (0)
FnCall: AstNode = {
    <name:Ident> "(" <maybe_args:FnCallArgs?> ")" => {
        AstNode::FnCall{
            name,
            args: match maybe_args {
                Some(a) => a,
                None => vec![],
            }
        }
    }
}

Bool: bool = {
    "true" => true,
    "false" => false,
}

Ident: String = {
    // TODO: Don't copy string, leave as reference for better memory usage
    <i:IdentTok> => i.to_string(),
}

Int: i32 = {
    <i:IntTok> => match i {
        Tok::Number(a) => a,
        _ => unreachable!(),
    }
}

Str: String = {
    <s:SingleLiteralString> => s[1..s.len()-1].to_string(),
    <s:DoubleLiteralString> => s[1..s.len()-1].to_string(),
}


extern {
    type Location = usize;
    type Error = ();

    enum Tok<'input> {
        "." => Tok::Dot,
        "=" => Tok::Equals,
        "+" => Tok::Plus,
        "-" => Tok::Minus,
        "/" => Tok::ForwardSlash,
        "*" => Tok::Star,
        ">" => Tok::GreaterThan,
        "<" => Tok::LessThan,
        "&" => Tok::Ampersand,
        "|" => Tok::BitwiseOr,
        "(" => Tok::LParen,
        ")" => Tok::RParen,
        "{" => Tok::LCurlyBracket,
        "}" => Tok::RCurlyBracket,
        "[" => Tok::LBracket,
        "]" => Tok::RBracket,
        "^" => Tok::Caret,
        "%" => Tok::Modulo,
        "!" => Tok::Exclamation,
        "~" => Tok::Tilde,
        "," => Tok::Comma,
        ";" => Tok::Semicolon,
        "==" => Tok::DoubleEquals,
        ">=" => Tok::GreaterThanEqual,
        "<=" => Tok::LessThanEqual,
        "<<" => Tok::ShiftLeft,
        ">>" => Tok::ShiftRight,
        "**" => Tok::Exponent,
        "->" => Tok::Arrow,
        "true" => Tok::True,
        "false" => Tok::False,
        "if" => Tok::If,
        "else" => Tok::Else,
        "elif" => Tok::Elif,
        "fn" => Tok::Fn,
        "return" => Tok::Return,
        "while" => Tok::While,
        "for" => Tok::For,
        "in" => Tok::In,
        "let" => Tok::Let,
        "break" => Tok::Break,
        "continue" => Tok::Continue,
        "match" => Tok::Match,
        "enum" => Tok::Enum,
        "struct" => Tok::Struct,
        "and" => Tok::And,
        "or" => Tok::Or,
        "not" => Tok::Not,
        SingleLiteralString => Tok::SingleLiteralString(<&'input str>),
        DoubleLiteralString => Tok::DoubleLiteralString(<&'input str>),
        IdentTok => Tok::Ident(<&'input str>),
        Crlf => Tok::Crlf,
        IntTok => Tok::Number(i32),
    }
}